(($) ->
  $.fn.ch_map = (callbacks, options) ->
    target = $(this)

    options = $.extend({
        autoload: false
        src: "<%= asset_path('ch_map.svg') %>"
        aspect_ratio: 0.62
    }, options );

    _toggleable_elements = ->
      elements = [
        target.find("#cantons>path").get()
        target.find("#canton_names text").get()
      ]
      return (e for e in ([].concat.apply([], elements)) when e?)

    _load_toggleable_elements = ->
      for event, callback of callbacks
        for element in _toggleable_elements()
          $(element).on(event, callback)

    _initialize_aspect_ratio_enforcement = ->
      enforce_aspect_ratio()
      $(window).resize( ->
        enforce_aspect_ratio()
      )

    enforce_aspect_ratio = ->
      target.css('height', target.width() * options.aspect_ratio)

    load = ->
      target.load(options.src, ->
        target.addClass('svg_loaded')
        _initialize_aspect_ratio_enforcement()
        _load_toggleable_elements()
      )

    load() if options.autoload
    return $(this)
) jQuery
